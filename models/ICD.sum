module ICD is

  //Data types for the ICD system
  BPM == -1 .. 300;
  MAX_PULSES == 0 .. 9;
  Joules == 0 .. 45;
  MAX_HISTORY == 5;
  FIBRILATION_PULSE == 30;
  TACHY_PULSE == 2;

  //A schema representing a heart
  schema Heart is
  dec
    //The heart rate
    rate : BPM;

    //The impulse to be administered to the heart 
    impulse : Joules
  end Heart;

  //A schema representing an impulse generator
  schema ImpulseGenerator is
  dec
    //Indicates whether the Pump is on
    is_on : bool;

    //The impulse to be administered to the heart at the next tick
    impulse : Joules
  end ImpulseGenerator;

  //A schema representing a heart rate monitor
  schema HRM is
  dec
    //Indicates whether the HRM is on
    is_on : bool;

    //The last reading from the heart
    rate : BPM
  end HRM;

  ICDStates ::= normal | fib | tachy;
  schema ICDS is
  dec
    is_on : bool;
    icdStates : ICDStates;
    heartRateHistory : seq BPM;
    tachyBPMLimit : BPM;
    OnTachyCure : bool;

    //limit the amount of pulses by creating a strict set
    pulseCount : MAX_PULSES;

    //The interval between pulses
    resetValue : nat;

    //The count till the next pulse
    countTicks: nat
  pred
    #heartRateHistory = MAX_HISTORY
  end ICDS;


 //The system state consists of a heart, a monitor, and an impulse generator
  schema state is
  dec
    heart : Heart;
    hrm : HRM;
    redundantHrm : HRM;
    generator : ImpulseGenerator;
    icd : ICDS
  end state;
  //Initially, the impulse is the minimum. The heart rate is
  //unconstrained, except that it cannot be negative
  schema HeartInit is
  dec
    heart' : Heart
  pred
    //The measured rate is just non-negative
    heart'.rate >= 0;
    heart'.impulse = min(Joules)
  end HeartInit;

  //Initially, the impulse is the minimum, and the generator is off
  schema ImpulseGeneratorInit is
  dec
    generator' : ImpulseGenerator
  pred
    not (generator'.is_on);
    generator'.impulse = min(Joules)
  end ImpulseGeneratorInit;

  //Initially, there are no readings, and the monitor is off
  schema HRMInit is
  dec
    hrm' : HRM;
    redundantHrm' : HRM
  pred
    not (hrm'.is_on and redundantHrm'.is_on);
    hrm'.rate = min(BPM);
    redundantHrm'.rate = min(BPM)
  end HRMInit;

  schema IcdInit is
  dec
    icd' : ICDS
  pred
    not (icd'.is_on);
    icd'.icdStates = normal;
    icd'.heartRateHistory = <min(BPM),min(BPM),min(BPM),min(BPM),min(BPM)>;
    icd'.tachyBPMLimit = 110;
    icd'.pulseCount = 9;
    icd'.resetValue = 0;
    icd'.countTicks = 0
  end IcdInit;

  schema init is

  pred
    HeartInit;
    ImpulseGeneratorInit;
    IcdInit;
    HRMInit
  end init;

   

  ////////////////////////////////////////////////////////////
  // Helper functions for the system
  ////////////////////////////////////////////////////////////

  //"Limit" an integer value to a set of values, by taking the min/max
  //of the set if the value is lower/higher than the min/max of that set
  //respectively
  //Examples: limit(-1, 1..10) = 1
  //          limit(5, 1..10) = 5
  //          limit(15, 1..10) = 10
  axiom is
  dec
    limit : int cross power int --> int
  pred
    forall i : int; ps : power int @ 
        (i < min(ps) => limit(i, ps) = min(ps)) and 
        (i >= min(ps) and i <= max(ps) => limit(i, ps) = i) and
        (i > max(ps) => limit(i, ps) = max(ps))
  end;

  ////////////////////////////////////////////////////////////
  // Operations for the Heart module
  ////////////////////////////////////////////////////////////

  axiom is
  dec
    HeartDefaultChange : BPM
  pred
    HeartDefaultChange = 20
  end;

  //For the purpose of animation, we specify a crude update:
  //increase the heart rate by a constant if no impulse is delivered; and
  //decrease the heart rate by the impulse if an impulse is delivered
  //The case when a positive impulse is delivered:
  op schema HeartTickNonZero is
  pred
    pre (heart.impulse > 0);
    heart'.rate = limit(heart.rate - heart.impulse, BPM);
    heart'.impulse = heart.impulse;
    changes_only {heart}
  end HeartTickNonZero;

  //The case when no impulse is delivered
  op schema HeartTickZero is
  pred
    pre (heart.impulse <= 0);
    heart'.rate = limit(heart.rate + HeartDefaultChange, BPM);
    changes_only {heart}
  end HeartTickZero;

  HeartTick == (HeartTickNonZero or HeartTickZero);

  ////////////////////////////////////////////////////////////
  // Operations for the ImpulseGenerator
  ////////////////////////////////////////////////////////////

  //Turn on the generator, but do not administer any impulses yet.
  op schema ImpulseGeneratorOn is
  pred
    generator'.is_on;
    generator'.impulse = generator.impulse;
    changes_only {generator}
  end ImpulseGeneratorOn;

  //Turn off the generator
  op schema ImpulseGeneratorOff is
  pred
    not (generator'.is_on);
    generator'.impulse = generator.impulse;
    changes_only {generator}
  end ImpulseGeneratorOff;

  //An operation for manually changing the impulse.
  //This is for example purposes only, and is not part of the requirements.
  op schema ImpulseGeneratorSetImpulseOn is
  dec
    impulse? : Joules
  pred
    pre (generator.is_on);
    generator'.impulse = impulse?;
    generator'.is_on = generator.is_on;
    changes_only {generator}
  end ImpulseGeneratorSetImpulseOn;

  //Attempt to set the current impulse when the generator is off
  op schema ImpulseGeneratorSetImpulseOff is
  dec
    impulse? : Joules
  pred
    pre (not (generator.is_on));
    changes_only {}
  end ImpulseGeneratorSetImpulseOff; 

  //Attempt to set the current impulse
  ImpulseGeneratorSetImpulse == 
    (ImpulseGeneratorSetImpulseOn or ImpulseGeneratorSetImpulseOff);

  //Get the current status of the generator
  op schema ImpulseGeneratorStatus is
  dec
    is_on! : bool;
    impulse! : Joules
  pred
    is_on! = generator.is_on;
    impulse! = generator.impulse;
    changes_only {}
  end ImpulseGeneratorStatus;

  //Tick the clock for the generator when the generator is on
  //The heart 'receives' the input
  op schema ImpulseGeneratorTickOn is
  pred
    pre (generator.is_on); 
    heart'.impulse = generator.impulse;
    heart'.rate = heart.rate;
    changes_only {heart}
  end ImpulseGeneratorTickOn;

  //Tick the clock for the generator when the generator is off
  op schema ImpulseGeneratorTickOff is
  pred
    pre (not (generator.is_on));
    changes_only {}
  end ImpulseGeneratorTickOff;

  //Tick the clock for the generator, providing a impulse to the heart
  //if the generator is on
  ImpulseGeneratorTick == (ImpulseGeneratorTickOn or ImpulseGeneratorTickOff);

  op schema IcdOn is
  pred
    pre (hrm'.is_on and generator'.is_on);
    icd'.is_on;
    icd'.icdStates = normal;
    icd'.heartRateHistory = <min(BPM),min(BPM),min(BPM),min(BPM),min(BPM)>;
    icd'.tachyBPMLimit = 110;
    icd'.pulseCount = 9;
    icd'.resetValue = 0;
    icd'.countTicks = 0
    changes_only{icd}
  end IcdOn;

  op schema IcdOff is
  pred
    pre (not(hrm'.is_on and generator'.is_on));
    not (icd'.is_on);
    changes_only{icd}
  end IcdOff;
  ////////////////////////////////////////////////////////////
  // Operations for the HRM module
  ////////////////////////////////////////////////////////////
 
  //Turn on the HRM, but do not start readings
  op schema HRMOn is
  pred
    hrm'.is_on;
    hrm'.rate = hrm.rate;
    redundantHrm'.is_on;
    redundantHrm'.rate = redundantHrm.rate;
    changes_only {hrm, redundantHrm}
  end HRMOn;

  //Turn off the HRM
  op schema HRMOff is
  pred
    not (hrm'.is_on and redundantHrm'.is_on);
    hrm'.rate = hrm.rate;
    redundantHrm'.rate = redundantHrm.rate;
    changes_only {hrm, redundantHrm}
  end HRMOff;

  //Models reading the heart's blood pressure when the generator is on
  op schema HRMGetPressureOn is
  pred
    pre (hrm.is_on and redundantHrm.is_on);
    hrm'.rate = heart.rate;
    redundantHrm'.rate = heart.rate;
    //the HRM system remains on if two readings are equal
    hrm'.is_on <=> (hrm'.rate = redundantHrm'.rate and hrm'.is_on);
    redundantHrm'.is_on <=> (hrm'.rate = redundantHrm'.rate and redundantHrm'.is_on);
    changes_only {hrm, redundantHrm}
  end HRMGetPressureOn;

  //Models reading the heart's blood pressure when the generator is off
  op schema HRMGetPressureOff is
  pred
    pre (not (hrm.is_on and redundantHrm.is_on));
    hrm'.rate = min(BPM);
    hrm'.is_on = hrm.is_on;
    redundantHrm'.rate = min(BPM);
    redundantHrm'.is_on = redundantHrm.is_on;
    changes_only {hrm, redundantHrm}
  end HRMGetPressureOff;

  //Get the current status of the HRM
  op schema HRMStatus is
  dec
    is_on! : bool;
    rate! : BPM
  pred
    is_on! = hrm.is_on;
    rate! = hrm.rate;
    changes_only {}
  end HRMStatus;

  //Tick the clock for the HRM, which reads the heart's blood
  //pressure if the HRM is on
  HRMTick == (HRMGetPressureOn or HRMGetPressureOff);

  ////////////////////////////////////////////////////////////
  // ICD Component code
  ////////////////////////////////////////////////////////////
  axiom is
  dec
    sum : seq int --> int;
    average : seq int --> int;
    deviation : seq int --> int
  pred
    sum(<>) = 0;
    forall ds : seq int | ds /= <> @  sum(ds) = head(ds) + sum(tail(ds));
    forall ds : seq int @ average(ds) = sum(ds) div #ds;
    deviation = average //////should be changed for real deviation
  end;

  axiom is
  dec
    sort : power int --> seq int
  pred
    sort({}) = <>;
    forall ps : power int | ps /= {} @ 
       sort(ps) = <min(ps)> ^ sort(ps diff {min(ps)})
  end;

  op schema IcdSettachyBPMLimitOn is
  dec
    rate? : BPM
  pred
    pre (icd.is_on);
    icd'.tachyBPMLimit = rate?;
    icd'.is_on = icd.is_on;
    changes_only{icd}
  end IcdSettachyBPMLimitOn;

  op schema IcdSettachyBPMLimitOff is
  dec
    rate? : BPM
  pred
    pre (not (icd.is_on));
    changes_only{}
  end IcdSettachyBPMLimitOff;

  IcdSettachyBPMLimit == (IcdSettachyBPMLimitOn or IcdSettachyBPMLimitOff);

  op schema fibrilationCure is
  pred
    pre (icd'.is_on and icd'.icdStates = fib);
    ImpulseGeneratorSetImpulse{FIBRILATION_PULSE/impulse?};
    changes_only{generator}
  end fibrilationCure;

  op schema setNextShock is
  pred
    pre (icd'.is_on and icd'.icdStates = tachy and not (icd'.pulseCount = 0) );
    icd'.countTicks = icd.resetValue;

    ImpulseGeneratorSetImpulse{TACHY_PULSE};

    changes_only{generator, icd}
  end setNextShock;

  op schema decrementTicks is
  pred
    pre (not (icd.countTicks = 0) and not (icd.pulseCount = 0));
    icd'.countTicks = icd.countTicks - 1
  end decrementTicks;


  op schema tachyCure is
  pred
    pre (icd'.is_on and not (icd'.icdStates = tachy));
    icd'.resetValue = 600 div icd.tachyBPMLimit + 15;
    icd'.icdStates = tachy;
    changes_only{icd}
  end tachyCure;

  op schema zeroImpulseIfNormalState is
  pred
    pre (icd'.is_on and icd'.icdStates = normal);
    generator'.impulse = min(Joules);
    changes_only{generator}
  end zeroImpulseIfNormalState;

  op schema ifHRMFailing is
  pred
    //turns off ICD if HRM failing
    //HRM fails if the reading is very much different from history
    (average(icd.heartRateHistory) - HRMStatus.rate! > 10) => not (icd'.is_on);
    changes_only{icd}
  end ifHRMFailing;

  op schema IcdTick is
  dec
    rate : BPM;
    impulse : Joules
  pred
    pre (icd'.is_on and hrm'.is_on and generator'.is_on);
    rate = HRMStatus.rate!;
    zeroImpulseIfNormalState;

    ifHRMFailing;
    icd'.heartRateHistory = tail(icd.heartRateHistory) ^ {(MAX_HISTORY,rate)};
    
    // determine if a fibbliration has occured
    icd'.icdStates = fib <=> ((deviation(icd'.heartRateHistory) > 5 and icd.icdStates = normal) 
      or (icd.icdStates = tachy and icd'.OnTachyCure)) => fibrilationCure;

    // if a shock count has expired reset it for the next shot count
    icd.countTicks = 0 and not (icd.pulseCount = 0) => setNextShock;

    // count until the next impulse
    not (icd.countTicks = 0) and not (icd.pulseCount = 0) => decrementTicks;

    rate > icd.tachyBPMLimit and not(icd.icdStates = tachy) => tachyCure;
    //icd'.icdStates = normal => ImpulseGeneratorSetImpulse{0/impulse?};
    //tachycardiaCure;
    changes_only{icd,generator}
  end IcdTick;

  ////////////////////////////////////////////////////////////
  // Bringing the components together
  ////////////////////////////////////////////////////////////
  

  On == (HRMOn s_compose ImpulseGeneratorOn s_compose IcdOn);
  Off == (HRMOff s_compose ImpulseGeneratorOff s_compose IcdOff);
  //The overall system tick
  Tick == (ImpulseGeneratorTick s_compose HeartTick s_compose HRMTick s_compose IcdTick)

end ICD

